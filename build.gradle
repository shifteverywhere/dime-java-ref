//
//  build.gradle
//  DiME - Data Identity Message Envelope
//  A powerful universal data format that is built for secure, and integrity protected communication between trusted
//  entities in a network.
//
//  Released under the MIT licence, see LICENSE for more information.
//  Copyright (c) 2024 Shift Everywhere AB. All rights reserved.
//
plugins {
    id 'java-library'
    id "org.sonarqube" version "3.4.0.2513"
    id 'maven-publish'
    id 'signing'
}

group 'io.dimeformat'
version '1.2.9'
description 'DiME (Data Integrity Message Envelope). A powerful universal data format that is built for secure, and integrity protected communication between trusted entities in a network using an application-based publik-key infrastructure (APKI).'

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    api 'com.goterl:lazysodium-java:5.1.4'
    api 'net.java.dev.jna:jna:5.14.0'
    api 'org.json:json:20240205'
    api 'org.slf4j:slf4j-simple:2.0.12'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

subprojects {
    tasks.withType(Javadoc).all { enabled = false }
}

test {
    useJUnitPlatform()
}

tasks.named('jar') {
    manifest {
        attributes('Implementation-Title': project.name,
                   'Implementation-Version': project.version)
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

publishing {
    publications {

        mavenJava(MavenPublication) {

            groupId = project.group
            artifactId = 'dime-java-ref'
            version = project.version

            from components.java

            pom {
                name = 'DiME'
                description = 'DiME (Data Integrity Message Envelope). A powerful universal data format that is built for secure, and integrity protected communication between trusted entities in a network using an application-based publik-key infrastructure (APKI).'
                url = 'https://docs.dimeformat.io'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'shifteverywhere'
                        name = 'Niclas Kjellin'
                        email = 'niclas@shifteverywhere.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/shifteverywhere/dime-java-ref.git'
                    developerConnection = 'scm:git:ssh://github.com/shifteverywhere/dime-java-ref.git'
                    url = 'http://github.com/shifteverywhere/dime-java-ref'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'OSSRH'
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = project.properties["ossrUsername"]
                password = project.properties["ossrPassword"]
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
