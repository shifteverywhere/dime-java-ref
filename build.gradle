//
//  build.gradle
//  DiME - Data Identity Message Envelope
//  A powerful universal data format that is built for secure, and integrity protected communication between trusted
//  entities in a network.
//
//  Released under the MIT licence, see LICENSE for more information.
//  Copyright (c) 2023 Shift Everywhere AB. All rights reserved.
//
plugins {
    id 'java-library'
    id "org.sonarqube" version "3.4.0.2513"
    id 'maven-publish'
    id 'signing'
}

group 'io.dimeformat'
version '1.2.6'
description 'DiME (Data Integrity Message Envelope). A powerful universal data format that is built for secure, and integrity protected communication between trusted entities in a network using an application-based publik-key infrastructure (APKI).'

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    api 'com.goterl:lazysodium-java:5.1.1'
    api 'net.java.dev.jna:jna:5.11.0'
    api 'org.json:json:20220320'
    api 'org.slf4j:slf4j-simple:2.0.3'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

subprojects {
    tasks.withType(Javadoc).all { enabled = false }
}

test {
    useJUnitPlatform()
}

tasks.named('jar') {
    manifest {
        attributes('Implementation-Title': project.name,
                   'Implementation-Version': project.version)
    }
}

java {
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

publishing {
    publications {

        mavenJava(MavenPublication) {

            groupId = 'io.dimeformat'
            artifactId = 'dime-java-ref'
            version = '1.2.6'

            from components.java

            pom {
                name = 'DiME'
                description = 'DiME (Data Integrity Message Envelope). A powerful universal data format that is built for secure, and integrity protected communication between trusted entities in a network using an application-based publik-key infrastructure (APKI).'
                url = 'https://docs.dimeformat.io'

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'http://www.opensource.org/licenses/mit-license.php'
                    }
                }
                developers {
                    developer {
                        id = 'shifteverywhere'
                        name = 'Niclas Kjellin'
                        email = 'niclas@shifteverywhere.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/shifteverywhere/dime-java-ref.git'
                    developerConnection = 'scm:git:ssh://github.com/shifteverywhere/dime-java-ref.git'
                    url = 'http://github.com/shifteverywhere/dime-java-ref'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'OSSRH'
            url = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = project.properties["ossrUsername"]
                password = project.properties["ossrPassword"]
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
